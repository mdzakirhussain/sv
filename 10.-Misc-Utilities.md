|S.no.|Topic|
|:-----|:----|
|1.|[UVM HDL routines](https://github.com/muneeb-mbytes/UVM_Course/wiki/10.-Misc-Utilities#hdl-access-routines)|
|2.|[UVM Pool](https://github.com/muneeb-mbytes/UVM_Course/wiki/10.-Misc-Utilities#uvm-pool)|
|3.|[TLM Comparer](https://github.com/muneeb-mbytes/UVM_Course/wiki/10.-Misc-Utilities#uvm_comparer)|

                               Tabular column.1. MISC Utilities topics


# HDL access routines
UVM provides the backdoor access sub-routines for force/release or read/deposit some vale on any hierarchical path provided in the argument. These functions acts as an interface between SystemVerilog and C code to perform various operations. These functions can not only be used for backdoor accesses, but also for forcing some value in any RTL modules.  

Following is a list of API’s(Application programming interface) that are provided by UVM to perform various things. We can directly call these functions from the testbench and provide proper string based path as an input argument. This will automatically invoke the DPI-C(Direct programming interface) code in UVM BCL(Base class library).    
The HDL access routine function are,
  
**1. uvm_hdl_check_path**  

Checks that the given HDL path exists are not.  

`import "DPI-C" function int uvm_hdl_check(string path);`  

In the Example shown below, the uvm_hdl_check_path function will check the given path exist are not in below code the path top.Ripple.A is exist, so it return the uvm_info as path exit, otherwise it will return the else condition.  
```
if(uvm_hdl_check_path("top.Ripple.A")) begin
`uvm_info("checking",$sformatf(" 1.check the path exist top.Ripple.A = %0d",top.Ripple.A),UVM_LOW)
end
else begin
`uvm_info("checking",$sformatf(" 1.The path does not exist top.Ripple.A = %0d",top.Ripple.A),UVM_LOW)
end
```
**2. uvm_hdl_deposit**

 Sets the given HDL path to the specified value.
```
import "DPI-C" function int uvm_hdl_deposit(string path, uvm_hdl_data_t value);
```
The Example shown below, the uvm_hdl_deposit function will set the given HDL path to the specified value, in below code the path **uvm_hdl_deposit("top.Ripple.B",2)** is set to the specified value 2 it is deposited in the HDL path, otherwise it will return the else condition.  
```
if(uvm_hdl_deposit("top.Ripple.B",2)) begin
`uvm_info("checking",$sformatf(" After Deposit of specified value top.Ripple.B = %0d",top.Ripple.B),UVM_LOW)
end  else begin
`uvm_info("checking",$sformatf(" Value is not Deposited to the Specified path top.Ripple.B = %0d",top.Ripple.B),UVM_LOW)   
end
```
**3. uvm_hdl_force**

 Forces the value on the given path. 
 ```               
 import "DPI-C" function int uvm_hdl_force(string path, uvm_hdl_data_t value);
```
**force** preserves the value on the signal until we explicitly call **release**. The Example shown below, the uvm_hdl_force function it will force the value on the given path. here the path is **uvm_hdl_force("top.Ripple.B",7)** it will force value '7' to the path **top.Ripple.B**. if path does not exist it will return the else statement.
```
         if(uvm_hdl_force("top.Ripple.B",7)) begin
          `uvm_info("checking",$sformatf(" Forced value of the specified path top.Ripple.B= %0d",top.Ripple.B),UVM_LOW)
           end
           else begin       
           `uvm_info("checking",$sformatf("value is not forced to the specified path top.Ripple.B= %0d",top.Ripple.B),UVM_LOW)
           end
```
**4. uvm_hdl_release**

 Release a value previously set with uvm_hdl_force. 
                
              import "DPI-C" function int uvm_hdl_release(string path);

Release used to help come out from the previous set forced value. The Example shown below, the uvm_hdl_release function it will release the previously forced value to the HDL specified path. here the path is **uvm_hdl_release("top.Ripple.B")** it will release the forced value '7' and take the assigned value of that particular path. if path does not exist it will return the else statement.
```
         if(uvm_hdl_release("top.Ripple.B"))begin
          `uvm_info("checking",$sformatf("After release the previous forced value it is able to take other values top.Ripple.B = 
           %0d",top.Ripple.B),UVM_LOW)
         end
         else begin
         `uvm_info("checking",$sformatf("forced value not released top.Ripple.B = %0d",top.Ripple.B),UVM_LOW)
```

**5. uvm_hdl_read**

 Gets the value at the given path. 
  ```              
             import "DPI-C" function int uvm_hdl_read(string path, output uvm_hdl_data_t value);
```
Gets the value at the specified  path. The Example shown below, the uvm_hdl_read function it will read the value in the specified path and display the   value of given path. here the path is **uvm_hdl_read("top.Ripple.C0",val)** it will read the value in path **top.Ripple.C0** and gives the value of that path. 
```
         uvm_hdl_read("top.Ripple.c0",val);
       `uvm_info("checking",$sformatf(" Reading the value in the specified path top.Ripple.C0 = %0d",val),UVM_LOW)
```
**Example:code snippet:**
```
          //-------------------------------------------------------
          // module 1-bit full adder
         //-------------------------------------------------------
          module fulladder(a,b,c,sum,carry);
         //-------------------------------------------------------
         // Declaration of input and output signals
         // of 1-bit full adder
        //-------------------------------------------------------
            input a;
            input b;
            input c;
            output sum;
            output carry;

        //continuous assignment statement
         assign sum = a^b^c;
         assign carry = (a&b) | (b&c) | (c&a);
         endmodule:fulladder

         //-------------------------------------------------------
           //module 4-bit Ripple carry adder using the 1-bit Fulladder
         //-------------------------------------------------------
         module bit4adder(input [3:0]A,
         //-------------------------------------------------------
         //Decalration of input and output signals
         //of 4-bit Ripple carry adder
        //-------------------------------------------------------
           input [3:0]B,
               input C0,
           output [3:0]S,
              output C4);

        //To connect between two 1-bit full adder
          wire C1,C2,C3;

        fulladder fa0(.a(A[0]),.b(B[0]),.c(C0),.sum(S[0]),.carry(C1));
        fulladder fa1(.a(A[1]),.b(B[1]),.c(C1),.sum(S[1]),.carry(C2));
        fulladder fa2(.a(A[2]),.b(B[2]),.c(C2),.sum(S[2]),.carry(C3));
        fulladder fa3(.a(A[3]),.b(B[3]),.c(C3),.sum(S[3]),.carry(C4));

        endmodule:bit4adder

           //Access for class uvm package
           import uvm_pkg::*;
      
           //Acess for uvm_macros
           `include "uvm_macros.svh"


        //-------------------------------------------------------
        // Interface module
         //-------------------------------------------------------
         interface inter();
          logic [3:0]A;
          logic [3:0]B;
          logic C0;
          logic [3:0]S;
          logic C4;

          endinterface

 
        //class test
        class test extends uvm_test;

          `uvm_component_utils(test)
           logic [3:0]val;

       function new(string name="test",uvm_component parent);
        super.new(name,parent);
       endfunction

        function void build_phase(uvm_phase phase);
         super.build_phase(phase);
        endfunction

        task run_phase(uvm_phase phase);
        super.run_phase(phase);

        phase.raise_objection(this);

        //Check specified HDL path exist
        if(uvm_hdl_check_path("top.Ripple.A")) begin
       `uvm_info("checking",$sformatf(" 1.check the path exist top.Ripple.intf.A = %0d",top.Ripple.A),UVM_LOW)
       end
       else begin
       `uvm_info("checking",$sformatf(" 1.The path does not exist top.Ripple.A = %0d",top.Ripple.A),UVM_LOW)
       end
      $display("");
      #15;


      `uvm_info("checking",$sformatf(" Before Depositing a specified value top.Ripple.B = %0d",top.Ripple.B),UVM_LOW)
      //set the given HDL path to a specified value
      if(uvm_hdl_deposit("top.Ripple.B",2)) begin
      `uvm_info("checking",$sformatf(" After Deposit of specified value top.Ripple.B = %0d",top.Ripple.B),UVM_LOW)
      end  else begin
      `uvm_info("checking",$sformatf(" Value is not Deposited to the Specified path top.Ripple.B = %0d",top.Ripple.B),UVM_LOW)
      end
      $display("");
      #15;  //30ns

      //get the value in the given path
      uvm_hdl_read("top.Ripple.B",val);
      `uvm_info("checking",$sformatf(" Reading the value in the specified path top.Ripple.B = %0d",val),UVM_LOW)
      $display("");
      #15;  //45ns

      //force the value to the specified path
      if(uvm_hdl_force("top.Ripple.B",7)) begin
      `uvm_info("checking",$sformatf(" Forced value of the specified path top.Ripple.B= %0d",top.Ripple.B),UVM_LOW)
      end
      else begin
      `uvm_info("checking",$sformatf("value is not forced to the specified path top.Ripple.B= %0d",top.Ripple.B),UVM_LOW)
      end
      $display("");
      #15;  //60ns

      //Release the value previous set by uvm_hdl_force
      if(uvm_hdl_release("top.Ripple.B"))begin
      `uvm_info("checking",$sformatf("After release the previous forced value it is able to take other values top.Ripple.B = %0d",top.Ripple.B),UVM_LOW)
      end
      else begin
      `uvm_info("checking",$sformatf("forced value not released top.Ripple.B = %0d",top.Ripple.B),UVM_LOW)
      end
      $display("");
      #5;  //65ns

      //Read the value from specified HDL path
      uvm_hdl_read("top.Ripple.A",val);
      `uvm_info("checking",$sformatf(" Reading the value in the specified path top.Ripple.A = %0d",val),UVM_LOW)
      $display("");

      #75 $finish;

      phase.drop_objection(this);

      endtask

      endclass

       //-------------------------------------------------------
       // Top Module
      //-------------------------------------------------------


     //run the test
     initial run_test("test");

     //instantiation of interface
     inter intf();

     //instatiation of 4bit Ripple carry adder  
     bit4adder Ripple(.A(intf.A), .B(intf.B), .C0(intf.C0), .S(intf.S), .C4(intf.C4));

    initial begin
    intf.A = 6;
    intf.B = 5;
    intf.C0 = 1;

    #15
    intf.A=9;
    intf.C0=1;


    #15  //30ns
    intf.A = 1;
    intf.B = 3;

    #15; //45ns
    intf.A =4;

    #10; //55ns
     intf.B=5;
     intf.A=2;

    #10; //65ns
    intf.B =3;
    intf.A=1;

    #1; //66ns
    `uvm_info("checking","Ending Simualtion now........",UVM_LOW)
    end

    endmodule:top
```


**Transcript output:**  

Initially at 0ns it will check the path exist are not. if path exist it will give the path value **top.Ripple.A = 6**. Now at #10ns, checking the value of B is 5, After at 15ns the value of 'B' is deposited using  function **uvm_hdl_deposit** this function deposit the value, which we are given in the path **uvm_hdl_deposit("top.Ripple.B",2)**. At 30ns we use a function **uvm_hdl_read("top.Ripple.B")** it is to read the value of the path so it 30ns it will read value of 'B' is 2. then At 45ns, we force the value by using the function **uvm_hdl_force("top.Ripple.B", 7)** it will force the value '7' to the path it will remain same through the path until we call for function **uvm_hdl_release("top.Ripple.B")**.At 60ns, It will release previous value which we are forced. then the variable 'B' updated with the other values which we are passing through top module, then at 65ns, we use the function **uvm_hdl_read("top.Ripple.A")** to read the value of the path.

 ![hdl_7_11](https://user-images.githubusercontent.com/110412474/200256468-bbba2114-5e6c-455a-8267-189134a512df.JPG)

                               Figure.1: Trancript output

**output waveform**

![hdl_7](https://user-images.githubusercontent.com/110412474/200480094-cc246f83-c131-45e9-ba1a-82df68150098.JPG)
 
                                Figure.2:output waveform of 4-bit Ripple carry adder
 
**Github lab link:** https://github.com/muneeb-mbytes/UVM_Course/blob/b7_team_kachori/Misc_Utilities/uvm_hdl_routines/uvm_misc.sv

**Github output link:** https://github.com/muneeb-mbytes/UVM_Course/blob/b7_team_kachori/Misc_Utilities/uvm_hdl_routines/top.log

---------
# UVM Pool 


The uvm_pool is a parameterized class that implements a pool data structure same as System Verilog associative array and it can be allocated based on demand. The items in the pool can be shared across different verification components as pass by reference. On copying the uvm_pool object, it copies the class handle instead of copying an entire associative array.

![POOL](https://user-images.githubusercontent.com/110443214/199667161-dc3065cc-28a9-4cbb-8bae-ccb27bd8e365.png)

                                 Figure.3.uvm_pool hierarchy


**uvm_pool class declaration:**

`class uvm_pool # (type KEY = int, T = uvm_void) extends uvm_object`


## uvm_pool methods

Method | Function | Description
-- | -- | --
**new** | function new (string name = "") | Creates a new pool with the given name
**get_global_pool** | static function this_type get_global_pool () | Returns the singleton global pool for the item type, T
**get_global** | static function T get_global (KEY key) | Returns the specified item instance from global pool
**get** | virtual function T get (KEY key) | Returns item with the given key if one exists, else a new item will be created and returned
**add** | virtual function void add (KEY key, T item) | Adds the given key value pair to the pool if key does not exist, else it will be overwritten
**num** | virtual function int num() | Returns the number of keys stored in the pool
**delete** | virtual function void delete (KEY key) | Removes the item with the given key from the pool
**exists** | virtual function int exists (KEY key) | Returns 1 if an item with the given key already exists in the pool, else returns 0
**first** | virtual function int first (ref KEY key) | Returns the key of the first item in the pool via ref argument
**last** | virtual function int last (ref KEY key) | Returns key of the last item in the pool via ref argument
**next** | virtual function int next (ref KEY key) | Returns key of the next item in the pool via ref argument
**prev** | virtual function int prev (ref KEY key) | Returns key of the previous item in the pool via ref argument

                               Tabular column.2. uvm_pool methods

**Code snippet**  

      class pool extends uvm_test;
      `uvm_component_utils(pool);

      /uvm_pool declaration
      uvm_pool #(string, int) pool_name; 
      string team;
      int T;

      //Default constructor
      function new(string name="pool",uvm_component parent=null);
      super.new(name,parent);
      endfunction

      virtual function void build_phase(uvm_phase phase);
      super.build_phase(phase);
      pool_name = new("id");
      endfunction

      virtual task run_phase(uvm_phase phase);

      // add method
      pool_name.add("BJT",1);
      pool_name.add("kachori", 2);
      pool_name.add("silicon_crew", 3);

      $display("BJT:1 , kachori:2 , silicon_crew:3");

      //Exists
      `uvm_info("test", $sformatf("team kachori is present = %0d",pool_name.exists("kachori")), UVM_LOW);
      `uvm_info("test", $sformatf("team five is not present = %0d",pool_name.exists("five")), UVM_LOW);


      //num method
      `uvm_info("test", $sformatf("Number of teams = %0d",pool_name.num()), UVM_LOW);

     //get method
     `uvm_info("test", $sformatf("ID for silicon_crew = %0d",pool_name.get("silicon_crew")), UVM_LOW);


     // prev method
     team= "silicon_crew";
     assert(pool_name.prev(team));
     `uvm_info("test", $sformatf("previous team of silicon_crew = %s", team), UVM_LOW);

     //next method
     team= "kachori";
     assert(pool_name.next(team));
     `uvm_info("test", $sformatf("next team after kachori  = %s", team), UVM_LOW);

      // first method
      assert(pool_name.first(team));
      `uvm_info("test", $sformatf("first team = %s", team), UVM_LOW);

      //last method
      assert(pool_name.last(team));
      `uvm_info("test", $sformatf("last team = %s", team), UVM_LOW);                                                                                                         
     //get 
      `uvm_info("test", $sformatf("get: pool_name[\"kachori\"] = %0d", pool_name.get("kachori")), UVM_LOW);                            

     //num
     `uvm_info("test", $sformatf("Number of entries in pool_name = %0d", pool_name.num()), UVM_LOW);

     // delete
     pool_name.delete("kachori");
     `uvm_info("test", $sformatf("After delete method: Number of entries in pool_name = %0d", pool_name.num()), UVM_LOW);
     endtask
     endclass


     module top;
     initial begin
     run_test("pool");
     end
     endmodule



Below figure shows the output of uvm_pool methods.  

![uvm_pool1](https://user-images.githubusercontent.com/110443214/199717582-c87093f0-d025-4969-9ca4-a825efd5c71d.png)

                                 Figure.4.Output of uvm_pool methods

* Items created in pool is BJT:1 , kachori:2 , silicon_crew:3.

* uvm_pool name given here is pool_name.

* pool_name.exists("kachori"), this will return 1 because kachori is there in pool.

* pool_name.exists("five"), this will return 0 because five is not there in pool

* pool_name.num(), this gives the number of items in pool, i.e 3.

* pool_name.get("silicon_crew"), this gives the id we're giving i.e 3.

* pool_name.prev(team), here the output is kachori, because before we've set team to silicon_crew, previous team of silicon_crew is kachori.

* pool_name.next(team), here the output is silicon_crew, because before we've set team to kachori , next team of kachori is silicon_crew.

* pool_name.first(team), this gives first item in pool, i.e BJT.

* pool_name.last(team), this gives last item in pool, i.e silicon_crew.

* pool_name.delete("kachori"), this will delete kachori from the pool.


**Github lab link:** https://github.com/muneeb-mbytes/UVM_Course/blob/b7_team_kachori/uvm_pool/pool_method/pool_method.sv  

**Github log file link:** https://github.com/muneeb-mbytes/UVM_Course/blob/b7_team_kachori/uvm_pool/pool_method/pool_method.log

---

# uvm_event

The uvm_event class is an extension of the uvm_event_base class. The uvm_event_base class is a wrapper class around the SystemVerilog event construct. uvm_event_base class is an abstract class.

Events are static objects useful for synchronization between the process. Event operations are of two staged processes in which one process will trigger the event, and the other processes will wait for an event to be triggered.  

![event1](https://user-images.githubusercontent.com/110443214/199667079-714bf152-02b2-4b1e-9f7a-f781620c928b.png)
                                            
                                 Figure.5.uvm_event hierarchy

**UVM Event Syntax**

       uvm_event event_name; // Event declaration

       event_name = new(); // Creating an event

       event_name.trigger();  // Triggering event

       event_name.wait_trigger(); // Waiting for event trigger    

# UVM_Object_string_pool #(T)  

This provides a specialization of generic uvm_pool #(KEY,T) class for an associative array of uvm_object based objects indexed by string. Specialization of uvm_object_string_pool class includes  uvm_event_pool. The uvm_event_pool means that uvm_object_string_pool storing uvm_event.   

![image](https://user-images.githubusercontent.com/110484152/200757872-0646d9e7-c544-4824-8e72-64a1fcb0ed6f.png)

                                             Figure.6: UVM_object_string_pool    


**class declaration**  

class uvm_object_string_pool #(type T  = uvm_object) extends uvm_pool #(string,T)

**UVM_OBJECT_STRING_POOL #(T) Methods**

sr. no. |**Methods**         |**Description**|  
|:--|:----------------------------------- | :-------------| 
|1.| function new ( string name = “”)    | Creates pool for the given name |      
|2.| static function T get_global (KEY key)     |Returns the specified item instance from the global item pool|   
|3.| static function this_type get_global_pool () | Returns the singleton global pool for the item of type T.y|  
|4.| virtual function string get_type_name()   | Returns type name of this object|  
|5.| virtual function T get (KEY key)  |Returns the object item for the given string key. If no item exists by that key, it creates a new item and returns the same for that key.|    
|6.| virtual function void delete ( KEY key)   | Removes the item from the pool for the given key.|   

   
                                           Tabular column.3: uvm_object_string_pool methods    

The uvm_event_pool is the specialization example of the uvm_object_string_pool. The uvm_event_pool maintains a pool of uvm_events that helps to share the uvm_event handle across various testbench components.  uvm_event_pool is a pool that stores the uvm_events. uvm_event is used to synchronize the two processes. If the processes to trigger and wait for a trigger of an event are running in different components then it is required to share the event handle across the components. The uvm_event class makes it easy by providing uvm_event_pool.     

Get the event handle from event pool  

`uvm_event instancename =  uvm_event_pool::get_global(“event_name”)`   

`get_global` : returns the specified item instance from the global item pool.    

code snippet:     

      class compA extends uvm_component;
      `uvm_component_utils(compA)
       uvm_event e1;

       function new(string name = "compA", uvm_component parent = null);
       super.new(name, parent);
       endfunction

       task run_phase(uvm_phase phase);
       super.run_phase(phase);
       e1 = uvm_event_pool::get_global("myevent");

      `uvm_info(get_type_name(),$sformatf("%0t: Before triggering the event", $time),UVM_LOW)
       #5;
       e1.trigger();
       `uvm_info(get_type_name(),$sformatf("%0t: After triggering the event", $time),UVM_LOW)
       endtask
       endclass

       class compB extends uvm_component;
       `uvm_component_utils(compB)
        uvm_event e1;

        function new(string name = "compB", uvm_component parent = null);
        super.new(name, parent);
        endfunction
 
        task run_phase(uvm_phase phase);
        super.run_phase(phase);
        e1 = uvm_event_pool::get_global("myevent");

       `uvm_info(get_type_name(),$sformatf("%0t: waiting for the event", $time),UVM_LOW)
        e1.wait_trigger();
       `uvm_info(get_type_name(),$sformatf("%0t: event is triggered", $time),UVM_LOW)
        endtask
        endclass

        class compC extends uvm_component;
        `uvm_component_utils(compC)
         uvm_event e2;

         function new(string name = "compC", uvm_component parent = null);
         super.new(name, parent);
         endfunction  

Testbench:  

         `include "uvm_macros.svh"
          import uvm_pkg::*;

          uvm_event_pool e_pool;
          //`include "components.sv"

          class base_test extends uvm_test;
          `uvm_component_utils(base_test)

          compA comp_a;
          compB comp_b;
          compC comp_c;

          function new(string name = "base_test",uvm_component parent=null);
          super.new(name,parent);
          e_pool = new("e_pool");
          endfunction : new

          function void build_phase(uvm_phase phase);
          super.build_phase(phase);
          comp_a = compA::type_id::create("comp_a", this);
          comp_b = compB::type_id::create("comp_b", this);
          comp_c = compC::type_id::create("comp_c", this);
          endfunction : build_phase

          function void end_of_elaboration();
          uvm_top.print_topology();
          endfunction

          task run_phase(uvm_phase phase);
          super.run_phase(phase);
          phase.raise_objection(this);
          #20;
          phase.drop_objection(this);
          endtask

          endclass


   Output:  

  ![pool ss](https://user-images.githubusercontent.com/110484152/200222577-722e7780-b246-45cd-8a18-dce87f7103cf.png)  

                                        Figure.8: Output of uvm_event_pool    

Explanation    

CompA triggers the event after 5 time-units and compB and compC waits for the event to be triggered. After 5 units  compA will be after trigger the event and compB and compC will get trigger the event   

`uvm_event_pool::get_global(“myevent”)` returns an event handle with the event name as “myevent”.    
The compB has uvm_event instance name as e1 and componentC has uvm_event instance name as e2. But both event instances refer to the same event.  

github code link: https://github.com/muneeb-mbytes/UVM_Course/tree/b7_team_kachori/Misc_Utilities/uvm_pool/uvm_event_pool

---

# uvm_comparer

* it's a standalone class used to set policy for doing comparisons and finds how miscompares are counted. every uvm object has compare() method which is performing the comparisons with other objects and results of comparison is stored in the comparer object.

* An object can be passed to set parameters like depth of comparison, verbosity, max number of miscompares etc - an extra layer of flexibility.

* it also has a set of comparison methods for integers, string, real numbers and objects.

## Variables

**1. policy** : Determines whether comparison is UVM_DEEP, UVM_REFERENCE, or UVM_SHALLOW.

`declaration : uvm_recursion_policy_enum policy = UVM_DEFAULT_POLICY`

**Example**

     //set policy 
     comp.policy = UVM_REFERENCE;

**2. show_max:** Sets the maximum number of messages to send to the printer for miscompares of an object.

`declaration : int unsigned show_max = 1`

**Example**

     //total number of miscompares to be printed
     comp.show_max = 10;


**3. verbosity:** Sets the verbosity for printed messages. This setting is used by the messaging mechanism to determine whether messages should be suppressed or shown.

`declaration : int unsigned verbosity = UVM_LOW`

**Example**

    //set verbosity
    comp.verbosity = UVM_LOW;



**4. sev:** Sets the severity for printed messages. This setting is used by the messaging mechanism for printing and filtering messages.

`declaration : uvm_severity sev = UVM_INFO`

**Example**

    //set severity
    comp.sev = UVM_ERROR;


**5. miscompares:** This string is reset to an empty string when a comparison is started. It holds the last set of miscompares that occurred during a comparison.

`declaration : string miscompares = ""`

**6. physical or abstract:** This bit provides a filtering mechanism for fields. The abstract and physical settings allow an object to distinguish between two different classes of fields. It is up to you, in the uvm_object::do_compare method, to test the setting of this field if you want to use the physical trait as a filter.

`declaration : bit physical = 1 or bit abstract = 1`

**7. check_type:** This bit determines whether the type, given by uvm_object::get_type_name, is used to verify that the types of two objects are the same. This bit is used by the compare_object method. In some cases it is useful to set this to 0 when the two operands are related by inheritance but are different types.

`declaration : bit check_type = 1`

**8. results:** This bit stores the number of miscompares for a given compare operation.

`declaration : int unsigned result = 0`

**Example**

    `uvm_info(get_full_name(), $sformatf("Comparing objects: result = %0d", comp.result), UVM_LOW)


## Methods

Method | Description
-- | --
**compare_field** | Compares two integral values.
**compare_field_int** | This method is the same as compare_field except that the arguments are small integers, less than or equal to 64 bits.
**compare_field_real** | This method is the same as compare_field except that the arguments are real numbers.
**compare_object** | Compares two class objects using the policy knob to determine whether the comparison should be deep, shallow, or reference.
**compare_string** | Compares two string variables.
**print_msg** | Causes the error count to be incremented and the message, msg, to be appended to the miscompares string (a newline is used to separate messages)

                               Tabular column.4. uvm_pool methods


## Methods definiations:

1. **compare_field** :   virtual function bit compare_field (  
                             string name,    
                             uvm_bitstream_t lhs,  
                             uvm_bitstream_t rhs,  
                             int size,  
                             uvm_radix_enum radix = UVM_NORADIX )

 
  name: used to store and print miscompare.  
  lhs and rhs: used to compare left-hand and right-hand side objects  
  size: indicates number of bis to compare. (size <= 4096)  
  radix: used for reporting purposes. Default radix = HEX.  

**Example**

    comp.compare_field("int_compare", 4'h3, 4'h6, 4);



2. **compare_field_int** :  virtual function bit compare_field (  
                             string name,    
                             uvm_bitstream_t lhs,  
                             uvm_bitstream_t rhs,  
                             int size,  
                             uvm_radix_enum radix = UVM_NORADIX )  

**Example**

    comp.compare_field_int("int_compare", 4'h2, 4'h4, 4);


3. **compare_object** :  virtual function bit compare_field (  
                             string name,    
                             uvm_object lhs,  
                             uvm_object rhs  )  


**Example**

    comp.compare_object("tr_compare", object1, object2);


4. **compare_string** :  virtual function bit compare_string (  
                             string name,    
                             string lhs,  
                             string rhs  )

**Example**

    comp.compare_string("string_compare", "team", "kachori");


**Code snippet**

**transaction**

    class transaction extends uvm_object;
      rand bit[15:0] addr;
      rand bit[15:0] data;
      `uvm_object_utils_begin(transaction)
      `uvm_field_int(addr, UVM_PRINT);
      `uvm_field_int(data, UVM_PRINT);
      `uvm_object_utils_end

      function new(string name = "transaction");
        super.new(name);
      endfunction
    endclass


**test**

    `include "uvm_macros.svh"
    import uvm_pkg::*;
    
    class base_test extends uvm_test;
    
      //object diclaration
      transaction object1, object2;
      uvm_comparer comp;

      `uvm_component_utils(base_test)

      //default constructor
      function new(string name = "base_test", uvm_component parent = null);
        super.new(name, parent);
      endfunction

      function void build_phase(uvm_phase phase);
        super.build_phase(phase);
        //creating object
        object1 = transaction::type_id::create("object1", this);
        object2 = transaction::type_id::create("object2", this);
        comp = new();
      endfunction

      task run_phase(uvm_phase phase);
        super.run_phase(phase);
        // randomizing object
        assert(object1.randomize());
        assert(object2.randomize());

        //set policy
        comp.policy = UVM_SHALLOW;
        //set verbosity
        comp.verbosity = UVM_LOW;
        //set severity
        //comp.sev = UVM_WARNING;
        //total number of miscompares to be printed
        comp.show_max = 10;
    
        `uvm_info(get_full_name(), "Comparing objects", UVM_LOW)
        //comparing two object
        void'(comp.compare_object("tr_compare", object1, object2));
        object2.copy(object1);
        //comparing objects after copy 
        void'(comp.compare_object("tr_compare", object1, object2));
        //printing result of miscompares
        `uvm_info(get_full_name(), $sformatf("Comparing objects: result = %0d", comp.result), UVM_LOW)
        
        //comparing two integer
        void'(comp.compare_field_int("int_compare", 4'h2, 4'h4, 4));
        //comparing strings
        void'(comp.compare_string("string_compare", "team", "kachori"));
        //comparing real 
        void'(comp.compare_field_real("real compare", 4.5,4.5));
   
       `uvm_info(get_full_name(), $sformatf("Comparing objects: result = %0d", comp.result), UVM_LOW)
   
       void'(comp.compare_field_int("int_compare", 4'h4, 4'h4, 4));
       void'(comp.compare_string("string_compare", "kachori", "kachori")); 
       void'(comp.compare_field_real("real compare", 4.5,6.5));

       `uvm_info(get_full_name(), $sformatf("Comparing objects: result = %0d", comp.result), UVM_LOW)

      endtask
    endclass



Below figure shows the output of uvm_comparer.  

![comparer](https://user-images.githubusercontent.com/110448382/200769932-6dc9a57b-d726-4711-b895-6512cb9e9fd4.png)

                                 Figure.9.Output of uvm_comparer

**Explanation:** Here in the output first objects are compared, there addr and data mismatch so it will give miscompare message as shown in the output.  
after result variable print miscompares is is 2 because 2 miscompares store inside result variable.  
for integer compare we pass argument as 2,4 and string also "team" and "kachori" is not equal so it print miscompare message and also result variable is increament to 4.  
but whenever two object match it will not increament the result variable as well as not print the miscompare message.

**Github lab link:**  https://github.com/muneeb-mbytes/UVM_Course/tree/b7_team_kachori/Misc_Utilities/uvm_comparer

**Github log file link:**  https://github.com/muneeb-mbytes/UVM_Course/blob/b7_team_kachori/Misc_Utilities/uvm_comparer/comparer_log.log
 
---


